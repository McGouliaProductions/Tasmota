>D
sel=0
up=0
dn=0
t:sto=30
to=60
LED=0
stI=0
PM1_0=0
PM2_5=0
PM4_0=0
PM10=0
NCPM0_5=0
NCPM1_0=0
NCPM2_5=0
NCPM4_0=0
NCPM10=0
TYPSIZ=0
rssi=0
IPadd=""
IPset=0
p:venue=""
p:grp=""
p:uID=""
;>B ;Boot Before Sensors
>BS ;Boot after Sensors Initialized
->Power1 1
->hsbcolor %0LED%,100,30
=#SCRNON1
;DONT FORGET TO CLEAN
=>sensor44 c
>S ;Every Second
=> status 8
+> status 11
=#LEDCHECK1
;Timer Ran out 
if sto==0
{
    =#SCRNOFF1
}
;Screen On
if sto!=-1
{
    =#DHOME1
}
else ;Screen Off
{
    if upd[grp]>0
    or upd[venue]>0
    {
        svars
        dt[c1l5f0s1]Venue: %venue%
        dt[c1l6f0s1]Group: %grp%
        =#SCRNON1
    }
}
;every 10 seconds
if stI==0
{
    +> status
}
else
{
    +> status %stI%
}
stI+=1
;Reset to 0
if stI==12
{
    stI=0
}
;Skip 8 and 9
if stI==8
{
    stI=10
}
;>R ;Executed on Restart
>T ;Sensor Telemetry every Read and Teleperiod
PM1_0=SPS30#PM1_0
PM2_5=SPS30#PM2_5
PM4_0=SPS30#PM4_0
PM10=SPS30#PM10
NCPM0_5=SPS30#NCPM0_5
NCPM1_0=SPS30#NCPM1_0
NCPM2_5=SPS30#NCPM2_5
NCPM4_0=SPS30#NCPM4_0
NCPM10=SPS30#NCPM10
TYPSIZ=SPS30#TYPSIZ
>b ;Button State Change
if bt[1]==0
{
    =#SCRNON1
}
if bt[2]==0
{
    =#SCRNON1
}
if bt[3]==0
{
    =#SCRNOFF1
}
>U ;JSON messages from cmd status  MUST be requested with +>
uID=StatusMQT#MqttClient
rssi=StatusSTS#Wifi#RSSI
IPadd=StatusNet#IPaddress
#LEDCHECK1
if wifis==1
{
    if mqtts==1
    {
        LED=120 ;Green
    }
    else
    {
        LED=60  ;Amber
    }
}
else
{
    LED=0   ;Red
}
if chg[LED]>0
{
    ->hsbcolor %0LED%,100,30
}
#DHOME1
;Edit carefully there is purposeful whitespace after dt[]
if upd[dvnm]>0
{
    dt[c1l1f0s2]%dvnm%
}
if lip!="(IP unset)"
and IPset!=1
{
    IPset=1
    dt[c1l3f0s1]%lip%               
    dt[c1l3f0s1]%lip%        
}
if upd[venue]>0
{
    svars
}
if upd[grp]>0
{
    svars
}
dt[c1l7f0s1]PM10 %PM10%   
if rssi>0
and chg[rssi]>0
{
    dt[c13l8f0s1]%0rssi%%%  
}
dt[c19l8] %0sto%   
#SCRNON1
sto=to
dt[z]
dt[c1l1f0s2]%dvnm%
dt[c1l3f0s1]%IPadd%
dt[c1l4f0s1]ID: %uID%
dt[c1l5f0s1]Venue: %venue%
dt[c1l6f0s1]Group: %grp%
dt[c1l7f0s1]PM10 %PM10%
dt[c1l8f0s1]Wifi Signal %0rssi%%%
dt[c1l7f0s1]PM10 %PM10%
dt[c19l8] %0sto% 
->Power2 1
#SCRNOFF1
sto=-1 ;disable timer
->Power2 0